scriptencoding utf-8

augroup allfiles
    "Wipe out the allfiles group for when we reload the vimrc. Otherwise we just keep reattaching commands
    autocmd!

    autocmd FileType markdown setlocal formatoptions+=t

    "consider '-' as part of the same word
    autocmd FileType markdown setlocal iskeyword+=- | setlocal spell

    "Customize PlantUML file settings
    autocmd FileType plantuml nnoremap <buffer> <leader>b :!plantuml -o %:p:h %<cr>

    "Customize python settings
    autocmd FileType python setlocal textwidth=79 | setlocal colorcolumn=80

    "Change lua tabstops to be more lua-esque
    autocmd FileType lua setlocal sw=2 sts=2 et

    "Close the preview window if it is open
    autocmd InsertLeave * pclose

    "Consdier .md files to be markdown and not modula2
    autocmd BufNewFile,BufReadPost *.md set filetype=markdown

    "Cleanup fugitive buffers automatically
    autocmd BufReadPost fugitive://* set bufhidden=delete

    " cursorline on active windows only
    autocmd VimEnter,WinEnter,BufWinEnter * setlocal cursorline | setlocal cursorcolumn
    autocmd WinLeave * setlocal nocursorline | setlocal nocursorcolumn
augroup END


function! SetupEnvironment()
    " Alternative to this approach is running with exrc, though this has security implications.
    " This approach is the approach suggested by Braam, and it turns out it is nice to see all of
    " the project settings in one file.
    let l:path = expand('%:p')

    " Does path contain the kernel_modules
    if l:path =~# 'acadia/kernel_modules'
        let g:linuxsty_patterns = ['/home/adam/projects/idexx/acadia/kernel_modules/']
        " use `bear make` for autocompletion and syntax checks
        setlocal listchars=tab:\»\ ,trail:·,extends:…
        setlocal noexpandtab
        setlocal tabstop=8
        setlocal shiftwidth=8
        setlocal textwidth=80
        setlocal colorcolumn=81
    elseif l:path =~# 'acadia/applications'
        let g:gutentags_ctags_exclude = ['node_modules']
        if &filetype ==? 'python'
            setlocal textwidth=100
            setlocal colorcolumn=101
        endif
    else
        let g:linuxsty_patterns = []
    endif
endfunction

augroup envswitch
    " Clean up envswitch group
    autocmd!
    autocmd VimEnter,WinEnter,BufWinEnter,BufReadPost,BufNewFile * call SetupEnvironment()
augroup END
